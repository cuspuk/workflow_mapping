from snakemake.utils import min_version

min_version("7.25.0")


include: "rules/common.smk"


module reads_workflow:
    snakefile:
        github("cuspuk/workflow_reads", path="workflow/Snakefile", tag=config["module_versions"]["reads"])
    config:
        config


use rule * from reads_workflow as reads_*


# Only include standalone rules if the module is used standalone, i.e. not as a part of a larger workflow
if not config.get("module_versions") or "mapping" not in config["module_versions"]:

    include: "rules/standalone.smk"


include: "rules/mapping.smk"
include: "rules/mapping/{tool}.smk".format(tool=config["mapping"]["mapping"])


if config["assembly"]["assembly"]:

    include: "rules/assembly/{tool}.smk".format(tool=config["assembly"]["assembly"])


if config["mapping"]["deduplication"]:

    include: "rules/deduplication/{tool}.smk".format(tool=config["mapping"]["deduplication"])


if config["mapping"]["_generate_qualimap"]:

    include: "rules/qualimap.smk"


wildcard_constraints:
    **get_constraints(),


rule all:
    input:
        **get_outputs(),
        **get_standalone_outputs(),
    default_target: True
    localrule: True
